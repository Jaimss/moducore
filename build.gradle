import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.4.21"
    id "net.minecrell.licenser" version "0.4.1"
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

// subprojects setup
allprojects {
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "net.minecrell.licenser"
    apply plugin: "com.github.johnrengelman.shadow"

    tasks.build.dependsOn tasks.updateLicenses

    // all subprojects have the same group/version as the main project
    group "dev.jaims.moducore"
    version "0.3.0"

    // shadow & set archive base name so only 1 jar generates
    jar {
        baseName "moducore-${projectDir.name}"
    }
    shadowJar {
        archiveClassifier.set("")
        baseName "moducore-${projectDir.name}"
        relocate "dev.jaims.mcutils", "dev.jaims.moducore.libs.dev.jaims.mcutils"
        relocate "net.dv8tion", "dev.jaims.moducore.libs.net.dv8tion.jda"
        relocate "com.okkero.skedule", "dev.jaims.moducore.libs.com.okerro.skedule"
        relocate "me.mattstudios.mf-msg-bukkit", "dev.jaims.moducore.libs.me.mattstudios.mf-msg-bukkit"
        relocate "org.jetbrains.annotations", "dev.jaims.moducore.libs.org.jetbrains.annotations"
        minimize()
    }

    // all projects have some shared dependencies & repositories
    repositories {
        mavenCentral()
        maven { url "https://repo.jaims.dev/repository/all/" } // all repositories
    }

    // dependencies required for all projects
    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib"

        testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    }

    // process resources to show the correct version
    processResources {
        with copySpec {
            from "src/main/resources"
            filter(ReplaceTokens, tokens: [
                    version: project.version
            ])
        }
    }

    // add license
    license {
        header = rootProject.file("HEADER.txt")
        include "**/*.kt", "**/*.java"
        newLine = true
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // kotlin options
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}
