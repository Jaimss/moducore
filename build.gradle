import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.4.21"
    id "me.bristermitten.pdm" version "0.0.30"
    id "net.minecrell.licenser" version "0.4.1"
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

defaultTasks "clean", "updateLicenses", "pdm", "build", "shadowJar"

// subprojects setup
subprojects {
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "me.bristermitten.pdm"
    apply plugin: "net.minecrell.licenser"
    apply plugin: "com.github.johnrengelman.shadow"

    // all subprojects have the same group/version as the main project
    group "dev.jaims.moducore"

    if (project == project(":api")) {
        version "0.0"
    } else {
        version "0.0.1"
    }

    // shadow & set archive base name so only 1 jar generates
    jar {
        baseName "moducore-${projectDir.name}"
    }
    shadowJar {
        archiveClassifier.set("")
        baseName "moducore-${projectDir.name}"
    }

    // all projects have some shared dependencies & repositories
    repositories {
        mavenCentral()
        maven { url "https://repo.jaims.dev/repository/all/" } // all repositories
    }

    // dependencies required for all projects
    dependencies {
        pdm "org.jetbrains.kotlin:kotlin-stdlib"
        pdm "com.google.code.gson:gson:2.8.6"

        testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    }

    // process resources to show the correct version
    processResources {
        with copySpec {
            from "src/main/resources"
            filter(ReplaceTokens, tokens: [
                    version: project.version
            ])
        }
    }

    // add license
    license {
        header = rootProject.file("HEADER.txt")
        include "**/*.kt", "**/*.java"
        newLine = true
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // kotlin options
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}