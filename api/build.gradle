plugins {
    id "org.jetbrains.dokka" version "1.4.20"
    id "maven-publish"
    id "java-library"
}

apply plugin: "maven-publish"
apply plugin: "org.jetbrains.dokka"

version "0.2"

dokkaHtml {
    outputDirectory.set(file("$projectDir/docs"))
    moduleName.set("moducore-api")
    dokkaSourceSets {
        configureEach {
            samples.from(file("$projectDir/../example/src/main/kotlin"))
            samples.from(file("$projectDir/../bukkit/src/main/kotlin"))
            externalDocumentationLink {
                url = new URL("https://hub.spigotmc.org/javadocs/spigot/")
            }
        }
    }
}

publishing {
    publications {
        mavenKt(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
        maven {
            def releasesRepoUrl = "https://repo.jaims.dev/repository/maven-releases/"
            def snapshotsRepoUrl = "https://repo.jaims.dev/repository/maven-snapshots/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.property("sonatype.user")
                password = project.property("sonatype.password")
            }
        }
    }
}

dependencies {
    api "dev.jaims.hololib:core:0.0.11"
    compileOnlyApi "org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT"
    api "dev.jaims.mcutils:bukkit:2.2.7"
    api "com.okkero.skedule:skedule:1.2.6"
    api "me.mattstudios:mf-msg-bukkit:2.0.1"
}

tasks.publish.dependsOn tasks.dokkaHtml, tasks.clean